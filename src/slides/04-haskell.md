
class: center, middle


Llenguatges de Programaci√≥

# Haskell

Albert Rubio, Jordi Petit

<br/>

![:height 10em](img/haskell.svg)

<br/>

Universitat Polit√®cnica de Catalunya, 2019

---

# Introducci√≥

- Llenguatge funcional pur.

- No hi ha:

  - assignacions,
  - bucles,
  - gesti√≥ mem√≤ria expl√≠cita.

- Hi ha:

  - *Lazy evaluation*: tractar estructures molt grans o infinites.

  - Sistemes de tipus potents.

    - tipus algebraics,
    - tipus polim√≤rfics,
    - infer√®ncia de tipus autom√†tica.

  - Funcions d'ordre superior. Funcions com a par√†metres.


---

# Hist√≤ria


- Al 1987 degut a la proliferaci√≥ de FPLs √©s decideix definir un
  Standard: Haskell.

- Al 1998 es crea una versi√≥ estable: Haskell98.

- El nom √©s en homenatge a Haskell B. Curry, pel seu treball en l√≤gica
  matem√†tica, base dels FPLs.

---

# Primer tast


## Factorial recursiu

```haskell
factorial :: Integer -> Integer

factorial 0 = 1
factorial n = n * factorial (n - 1)
```

--

## Factorial amb funci√≥ d'ordre superior

```haskell
factorial n = foldl (*) 1 [1..n]
```

--

## Quicksort

```haskell
quicksort :: Ord a => [a] -> [a]

quicksort []     = []
quicksort (p:xs) = (quicksort menors) ++ [p] ++ (quicksort majors)
    where
        menors = [x | x <- xs, x <  p]      -- filter (<  p) xs
        majors = [x | x <- xs, x >= p]      -- filter (>= p) xs
```



---

# Primer tast


## Arbres

```haskell
data Arbin a = Buit | Node a (Arbin a) (Arbin a)


alcada :: Arbin a -> Int

alcada Buit = 0
alcada (Node x fe fd) = 1 + max (alcada fe) (alcada fd)


preordre :: Arbin a -> [a]

preordre Buit = []
preordre (Node x fe fd) = [x] ++ preordre fe ++ preordre fd
```

--
## Entrada/sortida

```haskell
comptaLiniesAmbFoo = not . null . filter (== "Foo") . lines

main = do
    entr <- getContents
    let hiHaFoo = comptaLiniesAmbFoo entr
    print hiHaFoo
```




---

# Eines

Utilitzarem el *Glasgow Haskell Compiler* (GHC).

Funciona amb dos modes:

  - compilador (`ghc`): genera un executable

  - int√®rpret (`ghci`): obre un REPL (*Read‚Äìeval‚Äìprint loop*)

Inicialment usarem l'int√®rpret, despr√©s el compilador.


.cols5050[
.col1[
Instal¬∑laci√≥ Linux
```bash
üêß> sudo apt install ghc
```
]
.col2[
Instal¬∑laci√≥ Mac
```bash
üçé> brew install ghc
```
]
]

---

# Comandes de `ghci`


Invocaci√≥: `ghci`.

- `:?` invoca l'ajuda.

- `:load fitxer.hs` (`:l`) carrega codi font de `fitxer.hs`.

- `:reload` (`:r`) recarrega el darrer codi font.

- `:type expressi√≥` (`:t`) escriu el tipus de l'`expressi√≥`.

- `:quit` (`:q`, <kbd>control</kbd>+<kbd>d</kbd>) surt.



---

# Tipus predefinits b√†sics

|Tipus       |  Descripci√≥     | Literals      |
|:--------|:--------|:-------|
|`Bool`|Booleans|`False`, `True`|
|`Int`|Enters (32 o 64 bits Ca2)|`16`, `(-22)`|
|`Integer`|Enters (arbitr√†riament llargs)| `587326354873452644428` |
|`Char`|Car√†cters|`'a'`, `'A'`, `'\n'`|
|`Float`|Reals|`3.1416`, `1e-9`|

Exemples:

```haskell
3 :: Int
3 :: Integer
3.0 :: Float
```



---

# Tipus complexos

|Tipus       |  Descripci√≥     | Literals      |
|:--------|:--------|:-------|
|`a -> b`|Funcions|`Int -> Bool`|
|`(a, b, c)`|Tuples|`("Lleida", 23465, 'M')`|
|`[a]`|Llistes de `a`s|`[1, 4, 9, 16]`|
|`String`|Textos ‚â° `[Char]`|`"Jordi"`|

Les fletxes de les funcions s'associen per la dreta:

- `a -> b -> c` vol dir `a -> (b -> c)`.
- `a -> b -> c -> d` vol dir `a -> (b -> (c -> d))`.



---

# Operacions b√†siques

## Bool

Operadors habituals:

- ` not :: Bool -> Bool`
- `(&&) :: Bool -> Bool -> Bool`
- `(||) :: Bool -> Bool -> Bool`

Definici√≥ de la o-exclusiva:

```haskell
xOr :: Bool -> Bool -> Bool
xOr x y = (x || y) && not (x && y)
```

--

## Char

Funcions de conversi√≥: (cal un `import Data.Char`)

- `ord :: Char -> Int`
- `chr :: Int -> Char`


---

# Operacions b√†siques

## Operadors aritm√†tics (`Int`, `Integer`, `Float`)

- Suma: `+`
- Resta: `-`
- Multiplicaci√≥: `*`
- Divisi√≥: `/`, `div`, `rem`, `mod`
- Valor absolut: `abs`
- Conversi√≥ enter a real: `fromIntegral`
- Conversi√≥ real a enter: `round`, `floor`, `ceiling`
- Relacionals: `<`, `>`, `<=`, `>=`, `==`, `/=`

## Notaci√≥ prefixa/infixa

```haskell
2 + 3
(+) 2 3

div 9 4
9 `div` 4
```

- Els operadors s√≥n infixos ‚áí posar-los entre par√®ntesi per fer-los prefixos
- les funcions s√≥n prefixes ‚áí posar-les entre *backtits* per fer-les infixes




---

# Definici√≥ de funcions

Els identificadors de funcions comencen amb min√∫scula.
i les funcions poden tenir associada una declaraci√≥ de tipus.

```haskell
factorial :: Integer -> Integer
```

Si no hi √©s, Haskell infereix tot sol el tipus (inconvenient per a novells).

Les funcions es poden definir amb **patrons**:

```haskell
factorial 0 = 1
factorial n = n * factorial (n - 1)
```

o amb **guardes**:

```haskell
factorial n
    | n == 0    = 1
    | otherwise = n * factorial (n - 1)
```

L'avaluaci√≥ dels patrons i de les guardes entra per la primera
branca satisfactible. <br>‚áí Es pot assumir que les anteriors han fallat.

‚ö†Ô∏è La igualtat va despreÃÅs de cada guarda!



---

# Definici√≥ de funcions: Exemples

Fibonacci

```haskell
fibonacci :: Integer -> Integer

fibonacci 0 = 0
fibonacci 1 = 1
fibonacci n = fibonacci (n - 1) + fibonacci (n - 2)
```


Valor absolut

```haskell
valAbs :: Int -> Int

valAbs n
    | n >= 0    = n
    | otherwise = -n
```

Exponenciaci√≥

```haskell
elevat :: Int -> Int -> Int

x `elevat` 0 = 1
x `elevat` n = x * (x `elevat` (n - 1))
```



---

# Condicionals

La construcci√≥ `if-then-else` no √©s una instrucci√≥ sin√≥ una funci√≥ de tres par√†metres:

- un booleaÃÄ i dues expressions del mateix tipus,
- i retorna el resultat d'una de les dues expressions.

    ```haskell
    prod n m =
        if n == 0 then
            0
        else
            m + prod (n - 1) m
    ```

Similarment, el `case` tambeÃÅ eÃÅs una funcioÃÅ que permet aplicar discriminaci√≥
per patrons:

  - S'utilitza de forma semblant als patrons de les funcions per√≤
    amb qualsevol expressi√≥.

    ```haskell
    prod n m =
        case n of
            0 -> 0
            x -> m + prod (x - 1) m
    ```


---

# √ös de funcions

Els paraÃÄmetres no es passen, s'apliquen.

NO cal passar tots els parametres.

Totes les funcions tenen un uÃÅnic paraÃÄmetre.

Les funcions de m√©s d'un par√†metre retornen, en realitat, una nova funcioÃÅ.

Exemple:

`prod 3 5` eÃÅs, en realitat, `(prod 3) 5`

--

Primer apliquem 3 i el resultat eÃÅs un funcioÃÅ que espera un altre enter.

`prod :: Int -> Int -> Int`

--

`prod :: Int -> (Int -> Int)`

--

`(prod 3) :: (Int -> Int)`

--

`(prod 3) 5:: Int`




---

# Definicions locals

Per definir noms locals s'utilitza el `let-in`:

```haskell
prod n m =
    if n == 0 then 0
    else
        let x = div n 2
            y = mod n 2
            p = prod x m
        in
            if y == 0 then p + p
            else m + prod (n - 1) m
```

O el `where`:

```haskell
prod n m =
    if n == 0 then 0
    else
        if y == 0 then p + p
        else m + prod (n - 1) m
    where
        x = div n 2
        y = mod n 2
        p = prod x m
```

El `where` no √©s una expressi√≥ i el seu √†mbit es defineix per la indentaci√≥.


---

# Tuples

Una tupla √©s un tipus estructurat que genera un producte cartesi√† d'altres
tipus. Els camps soÃÅn de tipus heterogenis.

```Haskell
(3, "Girona", False) :: (Int, String, Bool)

descomposicioHoraria :: Int -> (Int, Int, Int)    -- hores, minuts i segons
descomposicioHoraria segons = (h, m, s)
    where
        h = div segons 3600
        m = div (mod segons 3600) 60
        s = mod segons 60
```

Per a tuples de dos elements, es pot accedir amb `fst` i `snd`:

  - `fst ('a', 12)` √©s `'a'`
  - `snd ('a', 12)` √©s `12`

Per a tuples generals, no hi ha definides funcions d'acc√©s
<br>‚áí Es poden crear o, millor, usar discriminaci√≥ per patrons:

.cols5050[
.col1[
```haskell
primer (x, y, z) = x
segon  (x, y, z) = y
tercer (x, y, z) = z
```

]
.col2[
```haskell
primer (x, _, _) = x
segon  (_, y, _) = y
tercer (_, _, z) = z
```

]
]



---

# Llistes

Una tupla √©s un tipus estructurat que cont√© una seq√º√®ncia
d'elements, tots del mateix tipus.

```haskell
[]
[3, 9, 27] :: [Int]
[[7], [3, 9, 27], [1, 5], []] :: [[Int]]
["Barcelona", "Brusel¬∑les"] :: [[Char]]
[1 .. 10]
[1, 3 .. 10]
```

Les llistes tenen dos **constructors**:

  - La llista buida: `[]`
  - Afegir per davant: `:` (amb `(:) :: a -> [a] -> [a]`)

La notaci√≥ `[16, 12, 21]` √©s una drecera per `16 : 12 : 21 : []`
que vol dir `16 : (12 : (21 : []))`.

En Haskell poden haver-hi llistes infinites (ja ho veurem).

---

# Llistes i patrons

La discriminaci√≥ per patrons permet **desconstruir** les llistes:

```haskell
suma :: [Float] -> Float

suma [] = 0.0
suma (x:xs) = x + suma xs
```

Diem que $e_1$ *matches* $e_2$ si existeix una substitucioÃÅ per les
variables de $e_1$ que la fan igual que $e_2$.

**Exemples**:

- `x:xs` *matches* `[2, 5, 8]` perqu√® `[2, 5, 8]` √©s `2 : (5 : 8 : [])`
  substitu√Ønt `x` amb `2` i `xs` amb `(5 : 8 : [])`
  que √©s `[5, 8]`.

- `x:xs` *does not match* `[]` perqu√® `[]` i `:` s√≥n constructors
  diferents.

- `x1:x2:xs` *matches* `[2, 5, 8]`
  substitu√Ønt `x1` amb `2`, `x2` amb `5`
  i `xs` amb `[8]`.

- `x1:x2:xs` *matches* `[2, 5]`
  substitu√Ønt `x1` amb `2`, `x2` amb `5`
  i `xs` amb `[]`.

**Nota:** El mecanisme de *matching* no √©s el mateix que el d'*unificaci√≥*
(Prolog).



---

# Llistes i patrons

Els patrons tamb√© es poden usar al `case` i al `where`.

```haskell
suma llista =
    case llista of
        [] -> 0
        (x:xs) -> x + suma xs

divImod n m
    | n < m      = (0, n)
    | otherwise  = (q + 1, r)
    where (q, r) = divImod (n - m) m
```


---

# Funcions predefinides sobre llistes

Vegeu [Funcions sobre llistes habituals en Haskell](https://xn--llions-yua.jutge.org/haskell/funcions-sobre-llistes.html).
